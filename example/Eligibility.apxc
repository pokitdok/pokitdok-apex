/**
 * The Eligibility class is a example driver of how to use the PokitDok API Client to run
 * an eligibility check on a patient.  It accomplishes this within its runEligibilityCheck()
 * method.   This example code can be executed in a Salesforce instance with a custom OnClick
 * JavaScript button on an object detail page configured like this:
 * 
 * {!REQUIRESCRIPT("/soap/ajax/25.0/connection.js")}
 * {!REQUIRESCRIPT("/soap/ajax/25.0/apex.js")}
 * var patientId='{!Patient__c.Name}';
 * var result = sforce.apex.execute("Eligibility","runEligibilityCheck",{patientID: patientId});
 * alert(result);
 * window.location.reload();
 * sforce.debug.trace=true;
 * 
 * The Client ID and Client Secret credentials passed into the API Client constructor are
 * obtained by signing up for free at https://platform.pokitdok.com.
 * 
 * Copyright (C) 2016, All Rights Reserved, PokitDok, Inc.
 * https://platform.pokitdok.com
 *
 * Please see the License.txt file for more information.
 * All other rights reserved.
 */
global with sharing class Eligibility {    
 
    // These are your credentials
    static final String CLIENT_ID = 'ClientID';
    static final String CLIENT_SECRET = 'ClientSecret';
    
    // Example activity status
    static final String COMPLETED = 'Completed';
    
    /**
     * The runEligibilityCheck() method instantiates the API Client, manufactures a JSON 
     * payload with the patient's PHI, executes the eligibility method and does something
     * meaningful with the JSON response.  
     * 
     * Two different approaches of using the APIs are demonstrated:  The first uses a 
     * convenience method (the eligibility() method) which POSTs to the /eligibility endpoint.
     * The second uses the more bare metal get() method which GETs data from the /activities 
     * endpoint.
     */
    webservice static String runEligibilityCheck(String patientID) {        
        // Use the PokitDok API Client to make an eligibility request (POST) using the convenience
        // function.
        PokitDokAPIClient pokitdok = new PokitDokAPIClient(CLIENT_ID, CLIENT_SECRET, true);
        String eligibilityRequest = buildEligibilityRequest(patientID);
        String eligibilityResponse = pokitdok.eligibility(eligibilityRequest);
        
        // If everything looks good, move along
        if (!responseHasErrors(eligibilityResponse)) {       
            // Get the activity ID of the eligibility transaction and look up its status
            String activityID = getActivityID(eligibilityResponse);
            
            // Use the PokitDok API Client to make an activites request (GET) using the raw
            // request method.  For the example, look up status of the aforementioned eligibility 
            // transaction.
            String activitiesRequestURL = PokitDokAPIClient.ACTIVITIES_ENDPOINT + activityID;
            String activitiesResponse = pokitdok.get(activitiesRequestURL);        
            
            if (!responseHasErrors(activitiesResponse) && 
                getActivityStatus(activitiesResponse) == COMPLETED) {
                // Do something meaningful with the response
                return getCoverageInformation(eligibilityResponse, patientID);
            }
        }
        
        return 'Unable to process your eligibility request at this time.  The ' +
               'original request has been logged.';
    }
    
    /**
     * The buildEligibilityRequest() method abstracts the extraction of the data necessary
     * data to build the JSON payload for the eligibility request, a POST request.  The PHI
     * may exist in one or many custom Salesforce objects.  For the purposes of this example,
     * the data is simply hardcoded and produces a JSON payload that looks like this:
     *
     * {
     *     "member": {
     *         "birth_date": "1970-01-01",
     *         "first_name": "John",
     *         "last_name": "Smith",
     *         "id": "3141592653"
     *     },
     *     "provider": {
     *         "first_name": "Jane",
     *         "last_name": "Doe",
     *         "npi": "1467560003"
     *     },
     *     "trading_partner_id": "MOCKPAYER"
     * }
     * 
     * @return the serialized JSON to send to the eligibility endpoint
     */
    private static String buildEligibilityRequest(String patientID) {        
        Patient__c patient = [select Id, Name, Birth_Date__c, First_Name__c, Last_Name__c,
                              Member_ID__c, Payer__c 
                              from Patient__c 
                              where Name = :patientID];
        
        String birthYear = String.valueof(patient.Birth_Date__c.year());
        String birthMonth = String.valueof(patient.Birth_Date__c.month());
        String birthDay = String.valueof(patient.Birth_Date__c.day());
        if(birthMonth.length() == 1) {
          birthMonth = '0' + birthMonth;
        }
        if(birthDay.length() ==1 ) {
          birthDay = '0' + birthDay;
        }
        String birthDate = birthYear + '-' + birthMonth +  + '-' + birthDay;
        
        // Create the object map
        Map<String,Object> eligibilityRequest = new Map<String,Object>();
        
        Map<String,String> member = new Map<String,String>();        
        member.put('birth_date', birthDate);
        member.put('first_name', patient.First_Name__c);
        member.put('last_name', patient.Last_Name__c);
        member.put('id', patient.Member_ID__c);
        eligibilityRequest.put('member',member);
        
        Map<String,String> provider = new Map<String,String>();
        provider.put('first_name', 'Jane');
        provider.put('last_name', 'Doe');
        provider.put('npi', '1467560003');
        eligibilityRequest.put('provider',provider);
        
        eligibilityRequest.put('trading_partner_id', patient.Payer__c);        
        
        // Serialize to JSON
        return JSON.serialize(eligibilityRequest);
    }
    
    /**
     * The getCoverageInformation parses the JSON response from the eligibility API
     * call and produces a meaningful message for the user.  It demonstrates how to pull
     * out a few fields in the JSON to construct a message.  In this case, the message
     * conveys to the user that the patient's insurance coverage is currently active, as
     * well as when coverage began under the plan.
     * 
     * @param response is the JSON response from the eligibilty request
     * @return some meaningful text to send back to the end user
     */
    private static String getCoverageInformation(String response, String patientID) {        
        // Find the patient record
        Patient__c patient = [select Id, Name, Birth_Date__c, First_Name__c, Last_Name__c,
                              Member_ID__c, Payer__c 
                              from Patient__c 
                              where Name = :patientID];
        
        String meaningfulResponse = '';
        Boolean isActive = false;
        Date planBeginDate = null;
        
        // Parse the response, extracting the active flag and the date that plan coverage
        // began.
        JSONParser parser = JSON.createParser(response);
        while (parser.nextToken() != null) {
            if ((parser.getCurrentToken() == JSONToken.FIELD_NAME) && 
                (parser.getText() == 'active')) {
                parser.nextToken();
                isActive = parser.getBooleanValue();
                patient.Active_Coverage__c = isActive;
            }
            if ((parser.getCurrentToken() == JSONToken.FIELD_NAME) && 
                (parser.getText() == 'plan_begin_date')) {
                parser.nextToken();
                planBeginDate = parser.getDateValue();
                patient.Plan_Begin_Date__c = planBeginDate;
            }
        }
        
        // Update the sObject
        update patient;
        
        // Asssemble the message
        if (isActive) {
            meaningfulResponse = 'The patient\'s coverage is active and began on ' + 
                                 planBeginDate.format();
        } else {
            meaningfulResponse = 'The patient\'s coverage has lapsed and is not active';
        }
        
        return meaningfulResponse;
    }
    
    /**
     * The getActivityID() method opens up the eligibility response and extracts the
     * actvity ID of the actual API transaction
     * 
     * @param eligibilityResponse is the JSON response from the eligibilty request
     * @return the activity ID
     */
    private static String getActivityID(String eligibilityResponse) {        
        String activityID = '';
        
        // Parse the eligibility response and extract the activity ID of the transaction
        JSONParser parser = JSON.createParser(eligibilityResponse);
        while (parser.nextToken() != null) {
            if ((parser.getCurrentToken() == JSONToken.FIELD_NAME) && 
                (parser.getText() == 'activity_id')) {
                parser.nextToken();
                activityID = parser.getText();
            }
        }
        
        return activityID;
    }
    
    /**
     * The getActivityStatus() method opens up the activities response and extracts the
     * activity status of the transaction in question.  The JSON fields in question aren't
     * unique, so the deserializeUntyped method is used for better precision.
     * 
     * @param activityResponse is the JSON response from the activity request
     * @return the activity status
     */
    private static String getActivityStatus(String activityResponse) {        
        String activityStatus = '';
        
        // Parse the activities response and extract the activity status
        Map <String, Object> root = (Map <String, Object>) JSON.deserializeUntyped(activityResponse);
        if (!root.isEmpty() && root.containsKey('data')) {
            Map <String, Object> data = (Map <String, Object>) root.get('data');
            if (!data.isEmpty() && data.containsKey('state')) {
            	Map <String, Object> state = (Map <String, Object>) data.get('state');
                if (!state.isEmpty() && state.containsKey('title')) {
                    activityStatus = (String) state.get('title');
                }
            }
    	}
        
        return activityStatus;
    }

    /**
     * The hasErrors() method opens up the API response and checks to see if there are error messages
     * in the response
     * 
     * @param response is the JSON response from the API request
     * @return True if there are errors
     */    
    private static Boolean responseHasErrors(String response) {        
        JSONParser parser = JSON.createParser(response);
        while (parser.nextToken() != null) {
            if ((parser.getCurrentToken() == JSONToken.FIELD_NAME) && 
                (parser.getText() == 'error') || (parser.getText() == 'errors')) {
                return True;
            }
        }
        
        return False;
    }    
}
