/**
 * APEX test classes are not permitted to access remote sites, so all tests of the API
 * calls need to be mocked.  The PokitDokMockResponseGenerator class implements the 
 * HttpCalloutMock interface and generate fake HTTPResponses for the various endpoints
 * of the PokitDok Platform.
 * 
 * Copyright (C) 2017, All Rights Reserved, PokitDok, Inc.
 * https://platform.pokitdok.com
 *
 * Please see the License.txt file for more information.
 * All other rights reserved.
 */
@isTest
public class PokitDokMockResponseGenerator implements HttpCalloutMock {
    
    // Test constants
    // GET
    public final static String ACTIVITIES_ID = '11111111';
    public final static String ACTIVITIES_NAME = 'eligibility';
    
    // Fetch access token
    public final static String ACCESS_TOKEN = 'POKIT_TOKEN';
    
    // Eligibility
    public final static String ELIGIBILITY_ACTIVITY_ID = '11111111';
    public final static String ELIGIBILITY_CORRELATION_ID = '99999999';
    
    // Boundary conditions
    public final static String UNSUCCESSFUL_MESSAGE = 'Unprocessable Entity';
    public final static String SESSION_EXPIRED_MESSAGE = 'Unauthorized';
    
    // Configures whether or not to handle error conditions
    public Boolean isExceptionThrown = false;
    public Boolean isResponseUnsuccessful = false;
    public Boolean hasSessionExpired = false;
    
    /**
     * The constructor allows the calling test code to configure the mock response
     * generator to handle error conditions.
     */
    public PokitDokMockResponseGenerator(Boolean isExceptionThrown, 
                                         Boolean isResponseUnsuccessful,
                                         Boolean hasSessionExpired) {
        this.isExceptionThrown = isExceptionThrown;
        this.isResponseUnsuccessful = isResponseUnsuccessful;
        this.hasSessionExpired = hasSessionExpired;
    }
    
    /**
     * The respond method returns a mocked HTTPResponse that can be tested by assertion.
     * The following endpoints are mocked:
     *   - authentication
     *   - eligibility
     * 
     * @param the request is the mocked up request that would be sent to the APIs
     * @return a mocked HTTPResponse
     */
    public HTTPResponse respond(HTTPRequest request) {
        HttpResponse response = new HttpResponse();
        
        // Throw an exception for testing an HTTP error
        if (isExceptionThrown) {
            CalloutException e = (CalloutException)CalloutException.class.newInstance();
            e.setMessage('Mock CalloutException thrown.');
            throw e;
        }
               
        // Create a mocked response for testing an unsuccessful response
        if (isResponseUnsuccessful) {
            return createUnsuccessfulResponse();   
        }
              
        // Create a mocked response for testing a session expiring
        if (hasSessionExpired) {
            return createSessionExpiredResponse();   
        }
 
        // Create a mocked response for testing GET requests
        if (request.getMethod() == PokitDokAPIClient.GET_METHOD) {
            response = createGetResponse();        
        }
        
        // Create a mocked response for testing authentication        
        if (request.getEndpoint() == PokitDokAPIClient.POKITDOK_BASE_URL + 
                                     PokitDokAPIClient.AUTHENTICATION_ENDPOINT) {                                         
            response = createAuthenticationResponse();
        }
        
        // Create a mocked response for testing an eligibility request, which tests
        // POST requests
        if (request.getEndpoint() == PokitDokAPIClient.POKITDOK_BASE_URL + 
                                     PokitDokAPIClient.ELIGIBILITY_ENDPOINT) {
            response = createEligibilityResponse();
        }
        
        return response;
    }
    
    /**
     * The createGetResponse method creates a mocked HTTPResponse to an endpoint
     * accepting GET requests.  Namely it creates a fake list of activities.
     * 
     * @return the HTTPResponse with the activities data
     */
    private HTTPResponse createGetResponse() {
        // Create a fake GET response
        HttpResponse response = new HttpResponse();
        response.setStatusCode(200);
        response.setHeader('Content-Type', 'application/json');

        Map<String,Object> getResponse = new Map<String,Object>();
        
        List<Map<String,Object>> activities = new List<Map<String,Object>>();
        Map<String,Object> activity = new Map<String,Object>();
        activity.put('id', ACTIVITIES_ID);
        activity.put('name', ACTIVITIES_NAME);
        activities.add(activity);
        
        getResponse.put('data', activities);       
        response.setBody(JSON.serialize(getResponse));
        
        return response;
    }
    
    /**
     * The createAuthenticationResponse method creates a mocked HTTPResponse to the
     * authentication endpoint.  Namely it creates a fake OAuth access token to be
     * passed in future API calls.
     * 
     * @return the HTTPResponse with the access token
     */
    private HTTPResponse createAuthenticationResponse() {
        // Create a fake authentication response
        HttpResponse response = new HttpResponse();
        response.setStatusCode(200);
        response.setHeader('Content-Type', 'application/json');

        Map<String,Object> authenticationResponse = new Map<String,Object>();
        authenticationResponse.put('access_token', ACCESS_TOKEN);       
        response.setBody(JSON.serialize(authenticationResponse));
        
        return response;
    }

    /**
     * The createEligibilityResponse method creates a mocked HTTPResponse to the 
     * eligibility endpoint.  Namely it creates a fake Activity ID and Correlation 
     * ID in the JSON.  These two pieces of data would be in every response returned
     * by the PokitDok Platform APIs.
     * 
     * @return the HTTPResponse with the Activity ID and Correlation ID
     */    
    private HTTPResponse createEligibilityResponse() {
        // Create a fake eligibility response
        HttpResponse response = new HttpResponse();
        response.setStatusCode(200);
        response.setHeader('Content-Type', 'application/json');        
        
        Map<String,Object> eligibilityResponse = new Map<String,Object>();
        
        Map<String,Object> meta = new Map<String,Object>();
        meta.put('activity_id', ELIGIBILITY_ACTIVITY_ID);
        eligibilityResponse.put('meta', meta);
        
        Map<String,Object> data = new Map<String,Object>();
        data.put('correlation_id', ELIGIBILITY_CORRELATION_ID);
        eligibilityResponse.put('data', data);    
        
        response.setBody(JSON.serialize(eligibilityResponse));
        return response;
    }

    /**
     * The createUnsuccessfulResponse method creates a mocked HTTPResponse to an 
     * API endpoint.  It returns a status code of 422, however, to exercise a failure
     * case.
     * 
     * @return the HTTPResponse with a 422 status code
     */
    private HTTPResponse createUnsuccessfulResponse() {
        // Create a fake unsuccessful authentication response
        HttpResponse response = new HttpResponse();
        response.setStatusCode(422);
        response.setHeader('Content-Type', 'application/json');

        Map<String,Object> authenticationResponse = new Map<String,Object>();
        authenticationResponse.put('errors', UNSUCCESSFUL_MESSAGE);       
        response.setBody(JSON.serialize(authenticationResponse));
        
        return response;
    }
    
    /**
     * The createSessionExpiredResponse method creates a mocked HTTPResponse to an 
     * API endpoint.  It returns a status code of 401, however, that lets the calling
     * code know that the session has expired and the token needs to be renewed.
     * 
     * @return the HTTPResponse with a 401 status code
     */
    private HTTPResponse createSessionExpiredResponse() {
        // Create a fake unsuccessful authentication response
        HttpResponse response = new HttpResponse();
        response.setStatusCode(401);
        response.setHeader('Content-Type', 'application/json');

        Map<String,Object> apiResponse = new Map<String,Object>();
        apiResponse.put('errors', SESSION_EXPIRED_MESSAGE);       
        response.setBody(JSON.serialize(apiResponse));
        
        return response;
    }    
}
